## platypusdefi

### 说明

本次的platypusdefi漏洞是一个很典型的借贷合约中的典型问题。借贷合约没有考虑到当前用户的借贷状态，导致用户在已经借贷的情况下，可以直接将之前抵押取出来，这样就凭空地获得了多余的代币信息。

### 基本信息

- Attacker：
  - 0xeff003d64046a6f521ba31f39405cb720e953958

- Attack Contract：
  - 0x67afdd6489d40a01dae65f709367e1b1d18a53

- Vulnerable Contract：
  - 0xff6934aac9c94e1c39358d4fdcf70aeca77d0ab0
  - 0xc007f27b757a782c833c568f5851ae1dfe0e6ec7
- Chain：Avalance
- Attack Tx
  - 0x1266a937c2ccd970e5d7929021eed3ec593a95c6
- Lost：8.5M

### 攻击分析

#### flashloan

![](https://spoock-note.oss-cn-shanghai.aliyuncs.com/img/202304032116621.png?x-oss-process=style/0W4Cpmt5GmWoL1jg8N4C)

攻击者通过闪电贷的方式，贷出44,000,000 USDC

#### deposit

攻击者将贷出的所有USDC，全部保存到Platypus.Finance: Pool中。

![](https://spoock-note.oss-cn-shanghai.aliyuncs.com/img/202304032140260.png?x-oss-process=style/0W4Cpmt5GmWoL1jg8N4C)

攻击者通过质押所有借贷的USDC，获得了44,000,100,592,104的LP-USDC。

攻击者然后又将自己所有获得的LP-USDC，全部质押到MasterPlatypusV4中。

![](https://spoock-note.oss-cn-shanghai.aliyuncs.com/img/202304032146965.png?x-oss-process=style/0W4Cpmt5GmWoL1jg8N4C)

#### borrow

攻击者通过positionView计算获得相关的借贷参数。如下所示：

![](https://images.oooverflow.com/2023/04/e5b0edd87ff0febe0dde92dd35738ccd.png)

最终确定可以借贷出来的金额是41,794,533,641,783,253,909,672,000

![](https://spoock-note.oss-cn-shanghai.aliyuncs.com/img/202304032204032.png?x-oss-process=style/0W4Cpmt5GmWoL1jg8N4C)

![](https://images.oooverflow.com/2023/04/19c9cc1429c5d0c77645b6e9d3c2952a.png)

攻击者又通过PlatypusTreasure合约的borrow函数进行借贷，PlaypusTreasure的借贷额度最高为质押的95%，借出了41,794,533,641,783,253,909,672,000个USP。

#### emergencyWithdraw

攻击者然后调用emergencyWithdraw紧急提款函数，最终攻击者提取的LP-USDC的数量是44,000,100,592,104。这个数量就是攻击者开始抵押到Platypus Finance：Pool中的。

![](https://images.oooverflow.com/2023/04/bd80819d8257a726cd32b6491c36a257.png)

分析提款的核心代码

```solidity
/// @notice Withdraw without caring about rewards. EMERGENCY ONLY.
/// @param _pid the pool id
function emergencyWithdraw(uint256 _pid) public nonReentrant {
    PoolInfo storage pool = poolInfo[_pid];
    UserInfo storage user = userInfo[_pid][msg.sender];

    if (address(platypusTreasure) != address(0x00)) {
        (bool isSolvent, ) = platypusTreasure.isSolvent(msg.sender, address(poolInfo[_pid].lpToken), true);
        require(isSolvent, 'remaining amount exceeds collateral factor');
    }

    // reset rewarder before we update lpSupply and sumOfFactors
    IBoostedMultiRewarder rewarder = pool.rewarder;
    if (address(rewarder) != address(0)) {
        rewarder.onPtpReward(msg.sender, user.amount, 0, user.factor, 0);
    }

    // SafeERC20 is not needed as Asset will revert if transfer fails
    pool.lpToken.transfer(address(msg.sender), user.amount);

    // update non-dialuting factor
    pool.sumOfFactors -= user.factor;

    user.amount = 0;
    user.factor = 0;
    user.rewardDebt = 0;

    emit EmergencyWithdraw(msg.sender, _pid, user.amount);
}
```

其中关键的代码是需要判断用户的抵押头寸是否有偿付能力的功能，即`platypusTreasure.isSolvent`。

```solidity
/**
    * @notice function to check if user's collateral position is solvent
    * @dev returns (true, 0) if the token is not a valid collateral
    * @param _user address of the user
    * @param _token address of the token
    * @param _open open a position or close a position
    * @return solvent
    * @return debtAmount total debt amount including interests
    */
function isSolvent(
    address _user,
    ERC20 _token,
    bool _open
) external view returns (bool solvent, uint256 debtAmount) {
    return _isSolvent(_user, _token, _open);
}


/**
    * @notice function to check if user's collateral position is solvent
    * @dev returns (true, 0) if the token is not a valid collateral
    * @param _user address of the user
    * @param _token address of the token
    * @param _open open a position or close a position
    * @return solvent
    * @return debtAmount total debt amount including interests
    */
function _isSolvent(
    address _user,
    ERC20 _token,
    bool _open
) internal view returns (bool solvent, uint256 debtAmount) {
    uint256 debtShare = userPositions[_token][_user].debtShare;

    // fast path
    if (debtShare == 0) return (true, 0);

    // totalDebtShare > 0 as debtShare is non-zero
    debtAmount = (debtShare * (totalDebtAmount + _interestSinceLastAccrue())) / totalDebtShare;
    solvent = debtAmount <= (_open ? _borrowLimitUSP(_user, _token) : _liquidateLimitUSP(_user, _token));
}
```

因为debtAmount表示的是借贷的数量，_borrowLimitUSP表示的是借贷限额。借贷数量是小于借贷限额的。所以solvent恒为True。因此，攻击者就可以提取所有的借贷数量，即44,000,100,592,104的LP-USDC。

到这一步，攻击者不仅将之前抵押在MasterPlatypusV4中的所有的LP-USDC全部提取出来，同时还通过borrow的方式**额外借贷了**417,945,336,417,832,539,096,720,0个USP。

### 总结

这个漏洞本质上是一个逻辑漏洞，因为合约在函数emergencyWithdraw()中仅仅只是考虑了用户是否能够足额借出代币，但是没有考虑用户当时的状态(是否已经借出了代币)。这样就导致攻击者在借贷之后，还可以通过emergencyWithdraw()的方式获得额外的USP代币。