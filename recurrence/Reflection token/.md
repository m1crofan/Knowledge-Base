## FDP

### 基本信息

Attack Tx：0x09925028ce5d6a54801d04ff8f39e79af6c24289e84b301ddcdb6adfa51e901b

LOSS: 16BNB

### 漏洞函数

#### balanceOf

```solidity
uint256 private constant MAX = ~uint256(0);

uint256 private _tTotal = 1000000000000000 * 10**8;

uint256 private _rTotal = (MAX - (MAX % _tTotal));


function _getCurrentSupply() private view returns(uint256, uint256) {
    uint256 rSupply = _rTotal;
    uint256 tSupply = _tTotal;      
    for (uint256 i = 0; i < _excluded.length; i++) {
        if (_rOwned[_excluded[i]] > rSupply || _tOwned[_excluded[i]] > tSupply) return (_rTotal, _tTotal);
        rSupply = rSupply.sub(_rOwned[_excluded[i]]);
        tSupply = tSupply.sub(_tOwned[_excluded[i]]);
    }
    if (rSupply < _rTotal.div(_tTotal)) return (_rTotal, _tTotal);
    return (rSupply, tSupply);
}


function _getRate() private view returns(uint256) {
    (uint256 rSupply, uint256 tSupply) = _getCurrentSupply();
    return rSupply.div(tSupply);
}

function tokenFromReflection(uint256 rAmount) public view returns(uint256) {
    require(rAmount <= _rTotal, "Amount must be less than total reflections");
    uint256 currentRate =  _getRate();
    return rAmount.div(currentRate);
}

function balanceOf(address account) public view override returns (uint256) {
    if (_isExcluded[account]) return _tOwned[account];
    // 资金金额需要结合rate来计算
    return tokenFromReflection(_rOwned[account]);
}
```

通过追溯balanceOf的实现，用户余额的计算公式是tokenFromReflection(_rOwned[account])。通过最终的函数变形，计算用户余额的计算公式变为：

```solidity
(_rOwned[account]).mul(_tTotal).div(_rTotal)
```

最终用户的余额与合约中的`_rTotal`、 `_tTotal`相关。如果可以修改这两者的值，最终就会影响用户的余额。

#### deliver

```solidity
function deliver(uint256 tAmount) public {
    address sender = _msgSender();
    require(!_isExcluded[sender], "Excluded addresses cannot call this function");
    (uint256 rAmount,,,,,) = _getValues(tAmount);
    _rOwned[sender] = _rOwned[sender].sub(rAmount);
    _rTotal = _rTotal.sub(rAmount);
    _tFeeTotal = _tFeeTotal.add(tAmount);
}


function _getValues(uint256 tAmount) private view returns (uint256, uint256, uint256, uint256, uint256, uint256) {
    (uint256 tTransferAmount, uint256 tFee, uint256 tBurn) = _getTValues(tAmount, _taxFee, _burnFee);
    uint256 currentRate =  _getRate();
    (uint256 rAmount, uint256 rTransferAmount, uint256 rFee) = _getRValues(tAmount, tFee, tBurn, currentRate);
    return (rAmount, rTransferAmount, rFee, tTransferAmount, tFee, tBurn);
}

function _getRValues(uint256 tAmount, uint256 tFee, uint256 tBurn, uint256 currentRate) private pure returns (uint256, uint256, uint256) {
    uint256 rAmount = tAmount.mul(currentRate);
    uint256 rFee = tFee.mul(currentRate);
    uint256 rBurn = tBurn.mul(currentRate);
    uint256 rTransferAmount = rAmount.sub(rFee).sub(rBurn);
    return (rAmount, rTransferAmount, rFee);
}
```

通过跟踪代码调用分析

- uint256 rAmount = tAmount.mul(currentRate)
- _rTotal = _rTotal.sub(rAmount) 等价于 _rTotal = _rTotal.sub(tAmount.mul(currentRate))

可以发现：

当有用户调用`deliver()`函数时，`_rTotal`减小，最终通过`balanceOf()`计算的用户余额就会变大。



## BIGFI

交易：[0x9fe19093a62a7037d04617b3ac4fbf5cb2d75d8cb6057e7e1b3c75cbbd5a5adc](https://bscscan.com/tx/0x9fe19093a62a7037d04617b3ac4fbf5cb2d75d8cb6057e7e1b3c75cbbd5a5adc)

### 漏洞函数

#### balanceOf

```solidity
uint256 private _tTotal;
uint256 private _rTotal;

function balanceOf(address account) public view override returns (uint256) {
    if (_isExcluded[account]) return _tOwned[account];
    return tokenFromReflection(_rOwned[account]);
}
function tokenFromReflection(uint256 rAmount) public view returns(uint256) {
    require(rAmount <= _rTotal, "Amount must be less than total reflections");
    uint256 currentRate =  _getRate();
    return rAmount.div(currentRate);
}

function _getRate() private view returns(uint256) {
    (uint256 rSupply, uint256 tSupply) = _getCurrentSupply();
    return rSupply.div(tSupply);
}

function _getCurrentSupply() private view returns(uint256, uint256) {
    uint256 rSupply = _rTotal;
    uint256 tSupply = _tTotal;      
    for (uint256 i = 0; i < _excluded.length; i++) {
        if (_rOwned[_excluded[i]] > rSupply || _tOwned[_excluded[i]] > tSupply) return (_rTotal, _tTotal);
        rSupply = rSupply.sub(_rOwned[_excluded[i]]);
        tSupply = tSupply.sub(_tOwned[_excluded[i]]);
    }
    if (rSupply < _rTotal.div(_tTotal)) return (_rTotal, _tTotal);
    return (rSupply, tSupply);
}
```

#### burn

```solidity
function burn(uint256 _value) public {
    _burn(msg.sender, _value);
}

function _burn(address _addr, uint256 _value) private {
    require(_value <= _rOwned[_addr]);
    _rOwned[_addr] = _rOwned[_addr].sub(_value);
    _tTotal = _tTotal.sub(_value);
    emit Transfer(_addr, dead, _value);
}
```

相比于之前的FDP漏洞，本次的漏洞函数是burn。直接通过burn()减少调用者的代币数量。最终实现的是：

- `_rOwned[_addr] = _rOwned[_addr].sub(_value)`;减少实际的代币数量
- `_tTotal = _tTotal.sub(_value)`，减少_tTotal的值。

再次梳理下计算余额的逻辑：

- balanceof: `rAmount.div(currentRate)`
- rAmount: `_tOwned[account]`
- `_currentRate`:`_rTotal/_tTotal`